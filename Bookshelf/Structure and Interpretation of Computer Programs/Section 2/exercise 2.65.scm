#lang sicp
(define (entry tree) (car tree))
(define (left-branch tree) (cadr tree))
(define (right-branch tree) (caddr tree))
(define (make-tree entry left right)
  (list entry left right))
(define (tree->list tree)
  (define (copy-to-list tree result-list)
    (if (null? tree)
        result-list
        (copy-to-list (left-branch tree) (cons (entry tree) (copy-to-list (right-branch tree) result-list)))))
  (copy-to-list tree '()))
(define (list->tree elements)
  (car (partial-tree elements (length elements))))
(define (partial-tree elts n)
  (if (= n 0)
      (cons '() elts)
      (let ((left-size (quotient (- n 1) 2)))
        (let ((left-result (partial-tree elts left-size)))
          (let ((left-tree (car left-result))
                (non-left-elts (cdr left-result))
                (right-size (- n (+ left-size 1))))
            (let ((this-entry (car non-left-elts))
                  (right-result (partial-tree (cdr non-left-elts)
                                              right-size)))
              (let ((right-tree (car right-result))
                    (remaining-elts (cdr right-result)))
                (cons (make-tree this-entry left-tree right-tree)
                      remaining-elts))))))))

(define (union-set tree1 tree2)
  (define (make-union set1 set2)
    (cond ((null? set1) set2)
          ((null? set2) set1)
          ((> (car set1) (car set2)) (cons (car set2) (make-union set1 (cdr set2))))
          ((< (car set1) (car set2)) (cons (car set1) (make-union (cdr set1) set2)))
          ((= (car set1) (car set2)) (cons (car set1) (make-union (cdr set1) (cdr set2))))))
  (list->tree (make-union (tree->list tree1) (tree->list tree2))))

(define (intersection-set tree1 tree2)
  (define (make-intersection set1 set2)
    (cond ((or (null? set1) (null? set2)) '())
          ((= (car set1) (car set2)) (cons (car set1) (make-intersection (cdr set1) (cdr set2))))
          ((> (car set1) (car set2)) (make-intersection set1 (cdr set2)))
          ((< (car set1) (car set2)) (make-intersection (cdr set1) set2))))
  (list->tree (make-intersection (tree->list tree1) (tree->list tree2))))

(define t1 (list 7 (list 3 (list 1 '() '()) (list 5 '() '())) (list 9 '() (list 11 '() '()))))
(define t2 (list 4 (list 2 (list 1 '() '()) (list 3 '() '())) (list 6 (list 5 '() '()) (list 7 '() '()))))

(union-set t1 t2)
(intersection-set t1 t2)